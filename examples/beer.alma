# 99 Bottles of Beer

# prints correct pluralization of bottles, leaves number on stack
func bottle/s [
    " bottle" print
    when*: [1 ≠] ["s" print]
    " " print
]

# prints "X bottle(s) " or "No more bottles " if 0, leaves number on stack
func quantified-bottles [
    if*: [0 >] [dup] ["No more"]
    print
    bottle/s
]

# int → , sings the song for n verses
func n bottles-of-beer [
    n
    while*: [0 ≥] [
        quantified-bottles "of beer on the wall" say
        quantified-bottles "of beer" say
        if*: [0 >] [
            "Take one down, pass it around" say
            1 -
            quantified-bottles "of beer on the wall!" say
            "" say # empty line between verses
        ] [
            "Go to the store, buy some more" say
            n quantified-bottles drop "of beer on the wall!" say
            1 -
        ]
    ]
    drop
]

func main [ 99 bottles-of-beer ]
