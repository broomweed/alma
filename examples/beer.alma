# 99 Bottles of Beer

func when* [ [] if* ]

# Vocabularies
func N [ if*: [0 >] [dup print] ["No more" print] ]
func bottles [ " bottle" print; when*: [1 ≠] ["s" print]; " " print ]
func of [ "of " print ]
func beer [ "beer " print ]
func on [ "on " print ]
func the [ "the " print ]
func wall [ "wall" print ]

func Take-one-down-pass-it-around [ "Take one down, pass it around" print 1 - ]
func Go-to-the-store-buy-some-more [ "Go to the store, buy some more" print drop ]

func ... [ "" say ]
func !!! [ "!" say drop 0 ]

# int → , sings the song for n verses
func bottles-of-beer [
    dup # save original number for end
    while*: [0 ≥] [
        N bottles of beer on the wall ...
        N bottles of beer ...

        if*: [0 >] [
            Take-one-down-pass-it-around ...
            N bottles of beer on the wall ...
            ...
        ] [
            Go-to-the-store-buy-some-more ...
            N bottles of beer on the wall !!!
        ]
    ]
    drop # remove the hanging 0
]

func main [ 99 bottles-of-beer ]
