# 99 Bottles of Beer

# prints correct pluralization of bottles
fn bottle/s [ if* [= 1] [print " bottle "] [print " bottles "] ]

# string int → int, prints "X bottle(s) <msg>"
fn quantified-bottles msg [
    if* [> 0] [print dup] [print "No more"]
    bottle/s
    println msg
]

# int → , sings the song for how-many verses
fn bottles-of-beer how-many [
    how-many | while* [≥ 0] [
        quantified-bottles "of beer on the wall"
        quantified-bottles "of beer"
        if* [> 0] [
            println "Take one down, pass it around"
            - 1
            quantified-bottles "of beer on the wall!"
            println "" # between-verse empty line
        ] [
            println "Go to the store, buy some more"
            print how-many
            println " bottles of beer on the wall!"
            - 1
        ]
    ]
    drop
]

fn main [ 99 | bottles-of-beer ]
